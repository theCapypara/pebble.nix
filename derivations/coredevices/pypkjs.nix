{
  lib,
  stdenv,
  python3Packages,
  fetchFromGitHub,
  fetchPypi,
  autoPatchelfHook,
  makeWrapper,
  zlib,
}:

let
  stpyv8 = python3Packages.buildPythonPackage rec {
    pname = "stpyv8";
    version = "13.1.201.22";

    src =
      let
        pyShortVersion = "cp" + builtins.replaceStrings [ "." ] [ "" ] python3Packages.python.pythonVersion;
      in
      fetchPypi {
        inherit pname version;
        format = "wheel";
        dist = pyShortVersion;
        python = pyShortVersion;
        abi = pyShortVersion;
        platform =
          ({
            x86_64-linux = "manylinux_2_31_x86_64";
            x86_64-darwin = "macosx_13_0_x86_64";
            aarch64-darwin = "macosx_14_0_arm64";
          }).${stdenv.hostPlatform.system};
        hash =
          ({
            x86_64-linux = "sha256-g0uXYbt/SdqLiHhHx2R0laLPbEX2niEkrg4/AkSTvBU=";
            x86_64-darwin = "sha256-tT32EUqIaY7m84IM9GR26D7gnJpn3Z989YymopKCOLA=";
            aarch64-darwin = "sha256-bLXodRruJIfMO18h6sbUWQQacYCneZQbZNtXNuJydu4=";
          }).${stdenv.hostPlatform.system};
      };

    pyproject = false;

    nativeBuildInputs = [
      python3Packages.pypaInstallHook
      python3Packages.wheelUnpackHook
    ]
    ++ (lib.optional stdenv.hostPlatform.isLinux autoPatchelfHook);

    buildInputs = [ zlib ];
  };

  pygeoip = python3Packages.buildPythonPackage rec {
    pname = "pygeoip";
    version = "0.3.2";

    src = fetchFromGitHub {
      owner = "appliedsec";
      repo = "pygeoip";
      tag = "v${version}";
      hash = "sha256-D058c3o+2rTMQJpgwvFKd5Qwt2j7u4+GFpQHjO7lOVQ=";
    };

    postPatch = ''
      rm Makefile
    '';

    pyproject = true;
    build-system = [ python3Packages.setuptools ];
  };
in
python3Packages.buildPythonPackage rec {
  pname = "pypkjs";
  version = "2.0.7";

  src = fetchFromGitHub {
    owner = "coredevices";
    repo = "pypkjs";
    tag = "v${version}";
    hash = "sha256-IL/8ELmEOHiCLOaD1LFk6Pc/W/25AzG83aU4UOr3BOA=";
  };

  nativeBuildInputs = [ makeWrapper ];

  propagatedBuildInputs = with python3Packages; [
    gevent
    gevent-websocket
    greenlet
    libpebble2
    netaddr
    peewee
    pygeoip
    pypng
    python-dateutil
    stpyv8
    requests
    sh
    six
    websocket-client
  ];

  pyproject = true;
  build-system = [ python3Packages.setuptools ];

  postFixup = ''
    wrapProgram $out/bin/pypkjs \
      --prefix LD_LIBRARY_PATH : ${lib.makeLibraryPath [ stdenv.cc.cc.lib ]}
  '';

  meta = with lib; {
    homepage = "https://github.com/pebble/pypkjs";
    description = "Python implementation of PebbleKit JS";
    license = licenses.mit;
    platforms = platforms.linux ++ platforms.darwin;
  };
}
